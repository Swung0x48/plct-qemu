#
# RISC-V translation routines for the XPulp Instruction Set.
#
# Copyright (c) 2021-2022 PLCT Lab.
#
# This program is free software; you can redistribute it and/or modify it
# under the terms and conditions of the GNU General Public License,
# version 2 or later, as published by the Free Software Foundation.
#
# This program is distributed in the hope it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for
# more details.
#
# You should have received a copy of the GNU General Public License along with
# this program.  If not, see <http://www.gnu.org/licenses/>.

# Fields
%rs2  20:5
%rs1  15:5
%rd    7:5
%l     7:1

# immediates:
%imm_i    20:s12
%imm_s    25:s7 7:5
%imm_s6   20:s5 25:1
%imm_u6   20:5 25:1

# Argument sets
&i    imm rs1 rd   !extern
&r    rd rs1 rs2   !extern
&s    imm rs1 rs2  !extern
&lhl_i   imm l
&lhl_r   rs1 l
&shl_r   imm rs1  l
&shl_i   imm imms l

# Formats
@i         ............   ..... ... .....  ....... &i      imm=%imm_i      %rs1 %rd
@r         .......  ..... ..... ... .....  ....... &r                 %rs2 %rs1 %rd
@s         .......  ..... ..... ... .....  ....... &s      imm=%imm_s %rs2 %rs1
@lhl_i     ............   ..... ... .... . ....... &lhl_i  imm=%imm_i                 %l
@lhl_r     ............   ..... ... .... . ....... &lhl_r                  %rs1       %l
@shl_r     ............   ..... ... .... . ....... &shl_r  imm=%imm_i      %rs1       %l
@shl_i     ............   ..... ... .... . ....... &shl_i  imm=%imm_i      imms=%rs1  %l
@sci       ............   ..... ... .....  ....... &i      imm=%imm_s6     %rs1 %rd
@sci_u     ............   ..... ... .....  ....... &i      imm=%imm_u6     %rs1 %rd

# *** Post-Incrementing Load & Store Instructions ***
cv_lb_ripi   ............   ..... 000 ..... 0001011  @i
cv_lbu_ripi  ............   ..... 100 ..... 0001011  @i
cv_lh_ripi   ............   ..... 001 ..... 0001011  @i
cv_lhu_ripi  ............   ..... 101 ..... 0001011  @i
cv_lw_ripi   ............   ..... 010 ..... 0001011  @i

cv_lb_rrpi   0000000 .....  ..... 111 ..... 0001011  @r
cv_lbu_rrpi  0100000 .....  ..... 111 ..... 0001011  @r
cv_lh_rrpi   0001000 .....  ..... 111 ..... 0001011  @r
cv_lhu_rrpi  0101000 .....  ..... 111 ..... 0001011  @r
cv_lw_rrpi   0010000 .....  ..... 111 ..... 0001011  @r

cv_sb_ripi   ....... .....  ..... 000 ..... 0101011  @s
cv_sh_ripi   ....... .....  ..... 001 ..... 0101011  @s
cv_sw_ripi   ....... .....  ..... 010 ..... 0101011  @s

cv_sb_rrpi   0000000 .....  ..... 100 ..... 0101011  @r
cv_sh_rrpi   0000000 .....  ..... 101 ..... 0101011  @r
cv_sw_rrpi   0000000 .....  ..... 110 ..... 0101011  @r

# *** Register-Register Load & Store Instructions ***
cv_lb_rr     0000000 .....  ..... 111 ..... 0000011  @r
cv_lbu_rr    0100000 .....  ..... 111 ..... 0000011  @r
cv_lh_rr     0001000 .....  ..... 111 ..... 0000011  @r
cv_lhu_rr    0101000 .....  ..... 111 ..... 0000011  @r
cv_lw_rr     0010000 .....  ..... 111 ..... 0000011  @r

cv_sb_rr     0000000 .....  ..... 100 ..... 0100011  @r
cv_sh_rr     0000000 .....  ..... 101 ..... 0100011  @r
cv_sw_rr     0000000 .....  ..... 110 ..... 0100011  @r

# *** Long Hardware Loop Setup instructions ***
cv_starti    ............  00000 000 0000 . 1111011  @lhl_i
cv_endi      ............  00000 001 0000 . 1111011  @lhl_i
cv_count     000000000000  ..... 010 0000 . 1111011  @lhl_r
cv_counti    ............  00000 011 0000 . 1111011  @lhl_i

# *** Short Hardware Loop Setup instructions ***
cv_setup     ............  ..... 100 0000 . 1111011  @shl_r
cv_setupi    ............  ..... 101 0000 . 1111011  @shl_i

# *** SIMD ALU instructions ***
cv_add_h          000000 0 ..... ..... 000 ..... 1010111  @r
cv_add_sc_h       000000 0 ..... ..... 100 ..... 1010111  @r
cv_add_sci_h      000000  ...... ..... 110 ..... 1010111  @sci
cv_add_b          000000 0 ..... ..... 001 ..... 1010111  @r
cv_add_sc_b       000000 0 ..... ..... 101 ..... 1010111  @r
cv_add_sci_b      000000  ...... ..... 111 ..... 1010111  @sci
cv_add_div2       011101 0 ..... ..... 010 ..... 1010111  @r
cv_add_div4       011101 0 ..... ..... 100 ..... 1010111  @r
cv_add_div8       011101 0 ..... ..... 110 ..... 1010111  @r
cv_sub_h          000010 0 ..... ..... 000 ..... 1010111  @r
cv_sub_sc_h       000010 0 ..... ..... 100 ..... 1010111  @r
cv_sub_sci_h      000010  ...... ..... 110 ..... 1010111  @sci
cv_sub_b          000010 0 ..... ..... 001 ..... 1010111  @r
cv_sub_sc_b       000010 0 ..... ..... 101 ..... 1010111  @r
cv_sub_sci_b      000010  ...... ..... 111 ..... 1010111  @sci
cv_sub_div2       011001 0 ..... ..... 010 ..... 1010111  @r
cv_sub_div4       011001 0 ..... ..... 100 ..... 1010111  @r
cv_sub_div8       011001 0 ..... ..... 110 ..... 1010111  @r
cv_avg_h          000100 0 ..... ..... 000 ..... 1010111  @r
cv_avg_sc_h       000100 0 ..... ..... 100 ..... 1010111  @r
cv_avg_sci_h      000100  ...... ..... 110 ..... 1010111  @sci
cv_avg_b          000100 0 ..... ..... 001 ..... 1010111  @r
cv_avg_sc_b       000100 0 ..... ..... 101 ..... 1010111  @r
cv_avg_sci_b      000100  ...... ..... 111 ..... 1010111  @sci
cv_avgu_h         000110 0 ..... ..... 000 ..... 1010111  @r
cv_avgu_sc_h      000110 0 ..... ..... 100 ..... 1010111  @r
cv_avgu_sci_h     000110  ...... ..... 110 ..... 1010111  @sci_u
cv_avgu_b         000110 0 ..... ..... 001 ..... 1010111  @r
cv_avgu_sc_b      000110 0 ..... ..... 101 ..... 1010111  @r
cv_avgu_sci_b     000110  ...... ..... 111 ..... 1010111  @sci_u
cv_min_h          001000 0 ..... ..... 000 ..... 1010111  @r
cv_min_sc_h       001000 0 ..... ..... 100 ..... 1010111  @r
cv_min_sci_h      001000  ...... ..... 110 ..... 1010111  @sci
cv_min_b          001000 0 ..... ..... 001 ..... 1010111  @r
cv_min_sc_b       001000 0 ..... ..... 101 ..... 1010111  @r
cv_min_sci_b      001000  ...... ..... 111 ..... 1010111  @sci
cv_minu_h         001010 0 ..... ..... 000 ..... 1010111  @r
cv_minu_sc_h      001010 0 ..... ..... 100 ..... 1010111  @r
cv_minu_sci_h     001010  ...... ..... 110 ..... 1010111  @sci_u
cv_minu_b         001010 0 ..... ..... 001 ..... 1010111  @r
cv_minu_sc_b      001010 0 ..... ..... 101 ..... 1010111  @r
cv_minu_sci_b     001010  ...... ..... 111 ..... 1010111  @sci_u
cv_max_h          001100 0 ..... ..... 000 ..... 1010111  @r
cv_max_sc_h       001100 0 ..... ..... 100 ..... 1010111  @r
cv_max_sci_h      001100  ...... ..... 110 ..... 1010111  @sci
cv_max_b          001100 0 ..... ..... 001 ..... 1010111  @r
cv_max_sc_b       001100 0 ..... ..... 101 ..... 1010111  @r
cv_max_sci_b      001100  ...... ..... 111 ..... 1010111  @sci
cv_maxu_h         001110 0 ..... ..... 000 ..... 1010111  @r
cv_maxu_sc_h      001110 0 ..... ..... 100 ..... 1010111  @r
cv_maxu_sci_h     001110  ...... ..... 110 ..... 1010111  @sci_u
cv_maxu_b         001110 0 ..... ..... 001 ..... 1010111  @r
cv_maxu_sc_b      001110 0 ..... ..... 101 ..... 1010111  @r
cv_maxu_sci_b     001110  ...... ..... 111 ..... 1010111  @sci_u
